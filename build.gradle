plugins {
    id "com.google.protobuf" version "0.8.12"
    id "com.diffplug.gradle.spotless" version "4.5.1"
    id 'ru.vyarus.use-python' version '2.2.0'
    id "org.openapi.generator" version "4.3.1"
    id 'com.github.johnrengelman.shadow' version '6.0.0'
    id "java"
    id 'jacoco'
}

allprojects {
    group = 'dev.feast'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'
    apply plugin: "com.diffplug.gradle.spotless"
    apply plugin: "jacoco"
    apply plugin: 'com.github.johnrengelman.shadow'

    shadowJar {
        zip64 true
    }

    test {
        finalizedBy jacocoTestReport // report is always generated after tests run
    }

    jacocoTestReport {
        dependsOn test // tests are required to run before generating the report
        reports {
            xml.enabled false
            csv.enabled false
            html.enabled true
        }
    }

    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url = uri('https://repo.maven.apache.org/maven2')
        }

        maven {
            url = uri('https://repo.spring.io/plugins-release')
        }
    }

    sourceCompatibility = '1.11'

    configurations.all {
    }

    java {
        withSourcesJar()
    }

    publishing {
        publications {
            maven(MavenPublication) {
                from(components.java)
            }
        }
    }

    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    spotless {
        groovyGradle {
            greclipse().configFile("$rootDir/greclipse.properties")
        }
        java {
            target project.fileTree(project.projectDir) {
               include "src/**/*.java"
            }
            googleJavaFormat()
        }
    }
    build.dependsOn shadowJar
}
